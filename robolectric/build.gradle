import org.robolectric.gradle.DeployedRoboJavaModulePlugin
import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin
apply plugin: DeployedRoboJavaModulePlugin

dependencies {
    annotationProcessor libs.auto.service
    annotationProcessor libs.error.prone.core

    api project(":annotations")
    api project(":junit")
    api project(":pluginapi")
    api project(":resources")
    api project(":sandbox")
    api project(":utils")
    api project(":utils:reflector")
    api project(":plugins:maven-dependency-resolver")
    api libs.javax.inject
    compileOnly libs.auto.service.annotations
    api libs.javax.annotation.api

    // We need to have shadows-framework.jar on the runtime system classpath so ServiceLoader
    //   can find its META-INF/services/org.robolectric.shadows.ShadowAdapter.
    api project(":shadows:framework")

    implementation libs.conscrypt.openjdk.uber
    api libs.bcprov.jdk18on
    compileOnly libs.findbugs.jsr305

    compileOnly AndroidSdk.MAX_SDK.coordinates
    compileOnly libs.junit4

    api "androidx.test:monitor:$axtMonitorVersion@aar"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion@aar"

    testImplementation libs.junit4
    testImplementation libs.truth
    testImplementation libs.truth.java8.extension
    testImplementation libs.mockito
    testImplementation libs.hamcrest.junit
    testImplementation "androidx.test:core:$axtCoreVersion@aar"
    testImplementation "androidx.test.ext:junit:$axtJunitVersion@aar"
    testImplementation "androidx.test.ext:truth:$axtTruthVersion@aar"
    testImplementation "androidx.test:runner:$axtRunnerVersion@aar"
    testImplementation libs.guava
    testCompileOnly AndroidSdk.MAX_SDK.coordinates // compile against latest Android SDK
    testRuntimeOnly AndroidSdk.MAX_SDK.coordinates // run against whatever this JDK supports
}

project.apply plugin: CheckApiChangesPlugin

checkApiChanges {
    from = [
            "org.robolectric:robolectric:${apiCompatVersion}@jar",
            "org.robolectric:annotations:${apiCompatVersion}@jar",
            "org.robolectric:junit:${apiCompatVersion}@jar",
            "org.robolectric:resources:${apiCompatVersion}@jar",
            "org.robolectric:sandbox:${apiCompatVersion}@jar",
            "org.robolectric:utils:${apiCompatVersion}@jar",
            "org.robolectric:shadowapi:${apiCompatVersion}@jar",
            "org.robolectric:shadows-framework:${apiCompatVersion}@jar",
    ]

    to = [
            project(":robolectric"),
            project(":annotations"),
            project(":junit"),
            project(":resources"),
            project(":sandbox"),
            project(":shadows:framework"),
            project(":utils"),
            project(":shadowapi"),
    ]

    entryPoints += "org.robolectric.RobolectricTestRunner"
    expectedChanges = [
            "^org.robolectric.util.ActivityController#",
            "^org.robolectric.util.ComponentController#",
            "^org.robolectric.util.ContentProviderController#",
            "^org.robolectric.util.FragmentController#",
            "^org.robolectric.util.IntentServiceController#",
            "^org.robolectric.util.ServiceController#",
    ]
}
