import com.android.build.api.dsl.ManagedVirtualDevice
import org.robolectric.gradle.AndroidProjectConfigPlugin

apply plugin: 'com.android.library'
apply plugin: AndroidProjectConfigPlugin

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        devices {
            // ./gradlew -Pandroid.sdk.channel=3 nexusOneApi28DebugAndroidTest
            nexusOneApi28(ManagedVirtualDevice) {
                device = "Nexus One"
                apiLevel = 28
                systemImageSource = "aosp"
                abi = "x86"
            }
            // ./gradlew -Pandroid.sdk.channel=3 nexusOneApi30DebugAndroidTest
            nexusOneApi30(ManagedVirtualDevice) {
                device = "Nexus One"
                apiLevel = 30
                systemImageSource = "aosp-atd"
                abi = "x86"
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    aaptOptions {
        noCompress 'txt'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/'
        String sharedTestSourceDir = sharedTestDir + 'java'
        String sharedTestResourceDir = sharedTestDir + 'resources'
        test.resources.srcDirs += sharedTestResourceDir
        test.java.srcDirs += sharedTestSourceDir
        androidTest.resources.srcDirs += sharedTestResourceDir
        androidTest.java.srcDirs += sharedTestSourceDir
    }
}

dependencies {
    implementation project(path: ':testapp')
    implementation project(path: ':shadowapi', configuration: 'default')

    testImplementation project(":robolectric")
    testImplementation "junit:junit:${junitVersion}"
    // temporarily use a newer monitor version for HardwareRendererCompat
    testImplementation("androidx.test:monitor:1.5.0-alpha03")
    testImplementation("androidx.test:runner:$axtVersion")
    testImplementation("androidx.test:rules:$axtVersion")
    testImplementation("androidx.test.ext:junit:$axtJunitVersion")
    testImplementation("androidx.test.ext:truth:$axtVersion")
    testImplementation("androidx.test:core:$axtVersion")
    testImplementation("com.google.truth:truth:${truthVersion}")
    testImplementation("com.google.guava:guava:$guavaJREVersion")

    // Testing dependencies
    // temporarily use a newer monitor version for HardwareRendererCompat
    androidTestImplementation("androidx.test:monitor:1.5.0-alpha03")
    androidTestImplementation("androidx.test:runner:$axtVersion")
    androidTestImplementation("androidx.test:rules:$axtVersion")
    androidTestImplementation("androidx.test.ext:junit:$axtJunitVersion")
    androidTestImplementation("androidx.test.ext:truth:$axtVersion")
    androidTestImplementation("com.google.truth:truth:${truthVersion}")
    androidTestImplementation("com.google.guava:guava:$guavaJREVersion")
}
