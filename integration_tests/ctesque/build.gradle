import org.robolectric.gradle.AndroidProjectConfigPlugin
import org.robolectric.gradle.GradleManagedDevicePlugin

apply plugin: 'com.android.library'
apply plugin: AndroidProjectConfigPlugin
apply plugin: GradleManagedDevicePlugin

android {
    compileSdk 33
    namespace 'org.robolectric.integrationtests.ctesque'

    defaultConfig {
        minSdk 16
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    aaptOptions {
        noCompress 'txt'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/'
        String sharedTestSourceDir = sharedTestDir + 'java'
        String sharedTestResourceDir = sharedTestDir + 'resources'
        test.resources.srcDirs += sharedTestResourceDir
        test.java.srcDirs += sharedTestSourceDir
        androidTest.resources.srcDirs += sharedTestResourceDir
        androidTest.java.srcDirs += sharedTestSourceDir
    }
}

dependencies {
    implementation project(':testapp')

    testImplementation project(':robolectric')
    testImplementation libs.junit4
    testImplementation libs.androidx.test.monitor
    testImplementation libs.androidx.test.runner
    testImplementation libs.androidx.test.rules
    testImplementation libs.androidx.test.ext.junit
    testImplementation libs.androidx.test.ext.truth
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.espresso.core
    testImplementation libs.truth
    testImplementation libs.guava

    // Testing dependencies
    androidTestImplementation project(':shadowapi')
    androidTestImplementation libs.androidx.test.monitor
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.ext.truth
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.truth
    androidTestImplementation libs.guava
    androidTestUtil libs.androidx.test.services
}
