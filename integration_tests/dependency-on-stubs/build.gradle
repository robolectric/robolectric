import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin

// test with a project that depends on the stubs jar, not org.robolectric:android-all

def findLatestAndroidStubsJar() {
  def androidHome = System.getenv("ANDROID_HOME") ?: ""
  if (androidHome.isEmpty()) {
    throw new IllegalStateException("ANDROID_HOME is not defined")
  }
  def platformsDir = new File("$androidHome/platforms")
  if (!(platformsDir.exists() && platformsDir.isDirectory())) {
    throw new IllegalStateException("ANDROID_HOME is missing platforms")
  }
  def latestPlatform = platformsDir.list().join(",").findAll(/android-(\d+)/) { match, group ->
    return group.toInteger()
  }.max()
  return "$androidHome/platforms/android-$latestPlatform/android.jar"
}

dependencies {
    api project(":robolectric")
    api libs.junit4

    testImplementation files(findLatestAndroidStubsJar())

    testCompileOnly AndroidSdk.MAX_SDK.coordinates // compile against latest Android SDK
    testImplementation libs.truth
    testImplementation libs.mockito
    testImplementation libs.hamcrest.junit
}
