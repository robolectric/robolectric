import org.robolectric.gradle.AndroidProjectConfigPlugin

apply plugin: 'com.android.library'
apply plugin: AndroidProjectConfigPlugin

android {
    compileSdk 33
    namespace 'org.robolectric.integrationtests.agp'

    defaultConfig {
        minSdk 16
        targetSdk 33
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    testOptions.unitTests.includeAndroidResources true
}

dependencies {
    // See change log at https://github.com/google/guava/releases/tag/v32.1.0.
    modules {
        module("com.google.guava:listenablefuture") {
            replacedBy("com.google.guava:guava", "listenablefuture is part of guava")
        }
    }
    // Testing dependencies
    testImplementation project(path: ':testapp')
    testImplementation project(":robolectric")
    testImplementation project(":integration_tests:agp:testsupport")

    testImplementation libs.junit4
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.runner
    testImplementation libs.androidx.test.ext.junit
}
