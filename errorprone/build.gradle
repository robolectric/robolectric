import org.gradle.internal.jvm.Jvm
import org.robolectric.gradle.DeployedRoboJavaModulePlugin
import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin
apply plugin: DeployedRoboJavaModulePlugin

// Disable annotation processor for tests
compileTestJava {
    options.compilerArgs.add("-proc:none")
}

test {
    enabled = false
}

dependencies {
    // Project dependencies
    implementation project(":annotations")
    implementation project(":shadowapi")

    // Compile dependencies
    implementation libs.google.errorprone.annotation
    implementation libs.google.errorprone.refaster
    implementation libs.google.errorprone.checkApi
    compileOnly libs.google.autoService.annotations
    compileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }

    annotationProcessor libs.google.autoService
    annotationProcessor libs.google.errorprone.core

    // in jdk 9, tools.jar disappears!
    def toolsJar = Jvm.current().getToolsJar()
    if (toolsJar != null) {
        compile files(toolsJar)
    }

    // Testing dependencies
    testImplementation libs.junit
    testImplementation libs.google.truth
    testImplementation(libs.google.errorprone.testHelpers) {
        exclude group: 'junit', module: 'junit' // because it depends on a snapshot!?
    }
    testCompileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }
}
