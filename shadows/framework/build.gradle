apply plugin: org.robolectric.gradle.RoboJavaModulePlugin
apply plugin: org.robolectric.gradle.DeployedRoboJavaModulePlugin

apply plugin: ShadowsPlugin

shadows {
    packageName "org.robolectric"
    sdkCheckMode "ERROR"
}

configurations {
    jni
}

task copyNatives(type: Copy) {
    outputs.dir file("${buildDir}/resources/main")

    project.afterEvaluate {
        configurations.jni.files.each { File file ->
            def nativeJarMatch = file.name =~ /lib.*-natives-(.*)\.jar/
            if (nativeJarMatch) {
                inputs.file file

                def platformName = nativeJarMatch[0][1]
                from(zipTree(file)) { rename { f -> "$platformName/$f" } }
            }

        }
    }

    into project.file("$buildDir/resources/main")
}

jar {
    dependsOn copyNatives
}

dependencies {
    api project(":annotations")
    api project(":resources")
    api project(":pluginapi")
    api project(":shadowapi")
    api project(":utils")
    api project(":utils:reflector")
    api "androidx.test:monitor:1.3.0-beta02@aar"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    api "com.almworks.sqlite4java:sqlite4java:0.282"
    compileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }
    api "com.ibm.icu:icu4j:53.1"
    api "androidx.annotation:annotation:1.1.0"
    api "com.google.auto.value:auto-value-annotations:1.6.2"
    annotationProcessor "com.google.auto.value:auto-value:1.6.2"

    jni "com.github.axet.litedb:libsqlite:0.282-3:natives-mac-x86_64"
    jni "com.github.axet.litedb:libsqlite:0.282-3:natives-linux-x86"
    jni "com.github.axet.litedb:libsqlite:0.282-3:natives-linux-x86_64"
    jni "com.github.axet.litedb:libsqlite:0.282-3:natives-windows-x86"
    jni "com.github.axet.litedb:libsqlite:0.282-3:natives-windows-x86_64"
}
