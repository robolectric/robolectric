import org.robolectric.gradle.DeployedRoboJavaModulePlugin
import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin
apply plugin: DeployedRoboJavaModulePlugin

apply plugin: ShadowsPlugin

shadows {
    packageName "org.robolectric"
    sdkCheckMode "ERROR"
}

configurations {
    sqlite4java
}

def sqlite4javaVersion = libs.versions.sqlite4java.get()

tasks.register('copySqliteNatives', Copy) {
    from project.configurations.sqlite4java {
        include '**/*.dll'
        include '**/*.so'
        include '**/*.dylib'
        rename { String filename ->
            def filenameMatch = filename =~ /^([^\-]+)-(.+)-${sqlite4javaVersion}\.(.+)/
            if (filenameMatch) {
                def platformFilename = filenameMatch[0][1]
                def platformFolder = filenameMatch[0][2]
                def platformExtension = filenameMatch[0][3]

                "${platformFolder}/${platformFilename}.${platformExtension}"
            }
        }
    }
    into project.file("$buildDir/resources/main/sqlite4java")
}

jar {
    dependsOn copySqliteNatives
}

javadoc {
    dependsOn copySqliteNatives
}

dependencies {
    api project(":annotations")
    api project(":nativeruntime")
    api project(":resources")
    api project(":pluginapi")
    api project(":sandbox")
    api project(":shadowapi")
    api project(":shadows:versioning")
    api project(":utils")
    api project(":utils:reflector")

    api "androidx.test:monitor:$axtMonitorVersion@aar"

    implementation libs.error.prone.annotations
    compileOnly libs.findbugs.jsr305
    api libs.sqlite4java
    compileOnly(AndroidSdk.MAX_SDK.coordinates)
    api libs.icu4j
    api libs.androidx.annotation
    api libs.auto.value.annotations
    annotationProcessor libs.auto.value

    sqlite4java libs.bundles.sqlite4java.native
}
