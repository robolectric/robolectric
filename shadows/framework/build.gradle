import org.robolectric.gradle.DeployedRoboJavaModulePlugin
import org.robolectric.gradle.RoboJavaModulePlugin

apply plugin: RoboJavaModulePlugin
apply plugin: DeployedRoboJavaModulePlugin

apply plugin: ShadowsPlugin

shadows {
    packageName "org.robolectric"
    sdkCheckMode "ERROR"
}

configurations {
    sqlite4java
}

task copySqliteNatives(type: Copy) {
    from project.configurations.sqlite4java {
        include '**/*.dll'
        include '**/*.so'
        include '**/*.dylib'
        rename { String filename ->
            def filenameMatch = filename =~ /^([^\-]+)-(.+)-${libs.versions.sqlite4java.get()}\.(.+)/
            if (filenameMatch) {
                def platformFilename = filenameMatch[0][1]
                def platformFolder = filenameMatch[0][2]
                def platformExtension = filenameMatch[0][3]

                "${platformFolder}/${platformFilename}.${platformExtension}"
            }
        }
    }
    into project.file("$buildDir/resources/main/sqlite4java")
}

jar {
    dependsOn copySqliteNatives
}

dependencies {
    api project(":annotations")
    api project(":nativeruntime")
    api project(":resources")
    api project(":pluginapi")
    api project(":sandbox")
    api project(":shadowapi")
    api project(":utils")
    api project(":utils:reflector")
    api libs.androidx.test.monitor

    implementation libs.google.errorprone.annotations
    compileOnly libs.google.findBugs
    api libs.sqlite4java.sqlite4java
    compileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }
    api libs.icu4j
    api libs.androidx.annotation
    api libs.google.autoValue.annotations
    annotationProcessor libs.google.autoValue

    sqlite4java libs.sqlite4java.libsqlite4java.osx
    sqlite4java libs.sqlite4java.libsqlite4java.linux64
    sqlite4java libs.sqlite4java.libsqlite4java.linux86
    sqlite4java libs.sqlite4java.libsqlite4java.win64
    sqlite4java libs.sqlite4java.libsqlite4java.win86
}
